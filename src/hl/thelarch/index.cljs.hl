(page "index.html"
  (:require
    [thelarch.zip :as zip]
    [thelarch.rpc :as rpc :refer [user]])
  (:require-macros
   [adzerk.env :as env]))

(rpc/init)

(env/def
  THELARCH_GH_BASIC_CLIENT_ID :required)

(def test-data
  [{:id 1 :text "my org mode document"}
   [{:id 2 :text "a child"}]
   [{:id 3 :text "another child"}
    [{:id 4 :text "inner dude the first"}]
    [{:id 6 :text "inner dude the second"}]
    [{:id 7 :text "inner dude the third"}]]
   [{:id 5 :text "yet another child"}]])

(def indexed (partial map-indexed list))

(defc mode :normal)
(defc tree (zip/zipper test-data))

(defmulti  keymap (fn [mode key] [mode key]))
(defmethod keymap [:normal "j"] [_ _] (swap! tree zip/next))
(defmethod keymap [:normal "k"] [_ _] (swap! tree zip/prev))
(defmethod keymap [:normal ">"] [_ _] (swap! tree zip/indent))
(defmethod keymap [:normal "<"] [_ _] (swap! tree zip/outdent))
(defmethod keymap [:normal "x"] [_ _] (swap! tree zip/delete))
(defmethod keymap [:normal "i"] [_ _] (dosync (swap! tree zip/create-left) (reset! mode :insert)))
(defmethod keymap [:normal "a"] [_ _] (dosync (swap! tree zip/create-right) (reset! mode :insert)))
(defmethod keymap [:normal "o"] [_ _] (dosync (swap! tree zip/create-child) (reset! mode :insert)))
(defmethod keymap [:normal "O"] [_ _] (dosync (swap! tree zip/create-parent) (reset! mode :insert)))
(defmethod keymap [:normal "c"] [_ _] (reset! mode :insert))
(defmethod keymap :default [m k] (.log js/console "No %s mode binding for key '%s'" (str m) k))

(defn ctrl-key [e]
  (let [k (.-which e)]
    (cond (= 13 k) (swap! mode #(get {:normal :insert} % :normal))
          (= 27 k) (reset! mode :normal)
          :else    (let [c (when (.-ctrlKey e) "C-")
                         m (when (.-metaKey e) "M-")]
                     (when (or c m) (keymap @mode (str c m (.fromCharCode js/String k))))))))

(defn cmd-key [e]
  (keymap @mode (.fromCharCode js/String (.-which e))))

(defn node
  [z]
  (cell-let [{:keys [id text editing] :as item} (cell= (zip/item z))]
    (let [kids     (cell= (indexed (zip/children z)))
          point?   (cell= (= item (zip/item tree)))
          editing? (cell= (and point? (= mode :insert)))
          classes  (cell= {:editing editing? :point point?})]
      (div (span
             :class classes
             :toggle (cell= (not editing?))
             :click #(when (= @mode :normal) (swap! tree zip/goto @z))
              text)
           (input :type "text"
                  :toggle editing?
                  :focus editing?
                  :value text
                  :keypress #(swap! tree zip/set-text @%))
           (ul (loop-tpl :bindings [[i kid] kids] (li (node kid))))))))

(html
  (head
    (link :href "https://maxcdn.bootstrapcdn.com/bootstrap/3.3.6/css/bootstrap.min.css" :rel "stylesheet"
      :integrity "sha384-1q8mTJOASx8j1Au+a5WDVnPi2lkFfwwEAa8hDDdjZlpLegxhjVME1fgjWPGmkzs7" :crossorigin "anonymous")
    (link :href "app.css" :rel "stylesheet"))
  (body :keyup ctrl-key :keypress cmd-key
    (nav :class "navbar navbar-inverse navbar-fixed-top"
      (div :class "container container-fluid"
        (div :class "navbar-header"
          (button :type "button" :class "navbar-toggle collapsed" :data-toggle "collapse" :data-target "#navbar" :aria-expanded "false" :aria-controls "navbar"
            (span :class "sr-only" "Toggle navigation")
            (span :class "icon-bar")
            (span :class "icon-bar")
            (span :class "icon-bar"))
          (span :class "navbar-brand" :href "#" "The Larch"))
        (div :id "navbar" :class "collapse navbar-collapse"
          (ul :class "nav navbar-nav"
            (li :class "active" (a :href "#" "Edit"))
            (li (a :href "#about" "Help"))
            (li (a :href "#contact" "About")))
          (button
            :toggle user
            :click #(do (rpc/logout!)
                        (set! (.-location js/window) (.-location js/window)))
            :class "navbar-text pull-right" "Log out")
          (span :class "navbar-text pull-right"
            (img :toggle user :src (cell= (:avatar_url user)))
            (cell= (if user
                     (:login user)
                     (button :type "submit" :class "btn btn-default"
                       :click #(set! (.-location js/window)
                                     (str "https://github.com/login/oauth/authorize?client_id="
                                          THELARCH_GH_BASIC_CLIENT_ID))
                       "Log in with GitHub")))))))
    (div :class "container"
      (div :class "main"
        (div :class "row"
          (div :class "span8"
            (node (cell= (zip/root tree))))
          (div :class "span4")
          )))))
